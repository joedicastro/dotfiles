" .vimrc
" Author: joe di castro <joe@joedicastro.com>
" Source: http://github.com/joedicastro/dotfiles/tree/master/vim
"
" Este fichero esta sujeto a cambios frecuentes. Conviene seguir los commits
" en el control de versiones.

" Establecer lenguaje {{{ ====================================================

" language en_US.UTF-8           " Soluciona problemas con algunos plugins

" }}}

" NEOBUNDLE {{{ ==============================================================

set nocompatible               " No a la compatibilidad completa con vi.

" Autoinstalacion y configuracion de NeoBundle {{{

" Autoinstalando NeoBundle
let iCanHazNeoBundle=1
let neobundle_readme=expand('~/.vim/bundle/neobundle.vim/README.md')
if !filereadable(neobundle_readme)
    echo "Installing NeoBundle.."
    echo ""
    silent !mkdir -p ~/.vim/bundle
    silent !git clone https://github.com/Shougo/neobundle.vim ~/.vim/bundle/neobundle.vim
    let iCanHazNeoBundle=0
endif

" Llama a NeoBundle
if has('vim_starting')
    set rtp+=~/.vim/bundle/neobundle.vim/
endif
call neobundle#rc(expand('~/.vim/bundle/'))

" dejemos que NeoBundle administre a NeoBundle (necesario!)
NeoBundle 'Shougo/neobundle.vim'
" }}}

" BUNDLES (Plugins gestionados a través de NeoBundle) {{{

" Shougo's way {{{

" Vimproc para ejecutar comandos de forma asincrona (NeoBundle, Unite)
NeoBundle 'Shougo/vimproc', {
      \ 'build' : {
      \     'windows' : 'make -f make_mingw32.mak',
      \     'cygwin' : 'make -f make_cygwin.mak',
      \     'mac' : 'make -f make_mac.mak',
      \     'unix' : 'make -f make_unix.mak',
      \    },
      \ }

" Unite. La interfaz para casi todo
NeoBundle 'Shougo/unite.vim'

" Unite sources
NeoBundleLazy 'Shougo/unite-outline', {'autoload':{'unite_sources':'outline'}}
NeoBundleLazy 'Shougo/unite-session', {'autoload':{'unite_sources':'session'}}
NeoBundleLazy 'tsukkee/unite-help', {'autoload':{'unite_sources':'help'}}
NeoBundleLazy 'ujihisa/unite-colorscheme', {'autoload':{'unite_sources':
            \'colorscheme'}}
NeoBundleLazy 'ujihisa/unite-locate', {'autoload':{'unite_sources':'locate'}}
NeoBundleLazy 'thinca/vim-unite-history', { 'autoload' : { 'unite_sources' :
            \['history/command', 'history/search']}}
NeoBundleLazy 'osyo-manga/unite-filetype', { 'autoload' : {'unite_sources' :
            \'filetype', }}
NeoBundleLazy 'osyo-manga/unite-quickfix', {'autoload':{'unite_sources':
            \'quickfix'}}
NeoBundleLazy 'osyo-manga/unite-fold', {'autoload':{'unite_sources':'fold'}}
NeoBundleLazy 'tacroe/unite-mark', {'autoload':{'unite_sources':'mark'}}
NeoBundle 'taka84u9/unite-git'
NeoBundle 'osyo-manga/vim-powerline-unite-theme'
" NeoBundleLazy 'tsukkee/unite-tag', {'autoload':{'unite_sources':'tag'}}
" NeoBundle 'Shougo/vimfiler'

" }}}

" Esquemas de color {{{

" Temas Oscuros
" Versión de molokai mejorada para terminal, casi identica a la de GUI
NeoBundle 'joedicastro/vim-molokai256'

NeoBundle 'tomasr/molokai'
NeoBundle 'sjl/badwolf'
NeoBundle 'nielsmadan/harlequin'

" Temas claros
NeoBundle 'vim-scripts/summerfruit256.vim'
NeoBundle 'joedicastro/vim-github256'

" Crear temas para terminal a partir de temas para GUI
" NeoBundle 'godlygeek/csapprox'

" }}}

" DCVS {{{
"
" Para gestionar Git
NeoBundle 'tpope/vim-fugitive'
" Para señalar los cambios del fichero con respecto al repo Git
NeoBundle 'airblade/vim-gitgutter'

" }}}

" Markdown {{{

" Sintaxis de Markdown
NeoBundleLazy 'joedicastro/vim-markdown'
" Previsualizacion de Markdown en el navegador, con soporte de Markdown Extra
NeoBundleLazy 'joedicastro/vim-markdown-extra-preview'

" }}}

" Utilidades de Linux {{{

" Algunos atajos para comandos Unix desde Vim
NeoBundle 'tpope/vim-eunuch'
" Permite realizar un diff entre directorios
NeoBundle 'joedicastro/DirDiff.vim'
" Edición hexadecimal
NeoBundle 'vim-scripts/hexman.vim'
" }}}

" Python {{{

" Autocompletado
NeoBundle 'Shougo/neocomplete.vim'
" Gran plugin para programar en Python
NeoBundleLazy 'klen/python-mode', {'autoload': {'filetypes': ['python']}}
" Manejar virtualenvs
NeoBundleLazy 'jmcantrell/vim-virtualenv', {'autoload': {'filetypes': ['python']}}
" Visualizar lineas de identado
NeoBundleLazy 'Yggdroot/indentLine', {'autoload': {'filetypes': ['python']}}
" Visualizar informes de coverage.py
NeoBundleLazy 'alfredodeza/coveragepy.vim', {'autoload': {'filetypes': ['python']}}


" }}}

" Snippets de código {{{

" Gestor de snippets avanzado y potente
NeoBundle 'SirVer/ultisnips'

" }}}

" Sintaxis {{{

NeoBundleLazy 'vim-scripts/JSON.vim', {'autoload': {'filetypes': ['json']}}
NeoBundleLazy 'vim-scripts/po.vim--gray', {'autoload': {'filetypes': ['po']}}
NeoBundleLazy 'joedicastro/vim-pentadactyl', {
            \ 'autoload': {'filetypes': ['pentadactyl']}}
NeoBundleLazy 'vim-scripts/crontab.vim', {'autoload': {'filetypes': ['crontab']}}
NeoBundle 'scrooloose/syntastic'

" }}}

" Abrir enlaces externos {{{

" Abrir una url en el navegador u otro tipo de ficheros con una app externa
NeoBundle 'vim-scripts/utl.vim'

" }}}

" Edición de texto {{{

" Autocompletado de (, [, {, ', ", ...
NeoBundle 'kana/vim-smartinput'
" Cambio de fechas rápido e inteligente
NeoBundle 'tpope/vim-speeddating'
" genial plugin para insertar/cambiar/eliminar pares de caracteres en torno a un
" objeto
NeoBundle 'tpope/vim-surround'
" permite repetir ciertas operaciones con la tecla 'dot'
NeoBundle 'tpope/vim-repeat'
" conmutar entre lineas de comentario
NeoBundle 'tpope/vim-commentary'
" inserción inteligente de digrafos
NeoBundle 'Rykka/easydigraph.vim'
" para navegar por el arbol de niveles de deshacer de vim
NeoBundle 'sjl/gundo.vim'
" para insertar bloques de lorem ipsum
NeoBundle 'vim-scripts/loremipsum'
" para conocer toda la informacion sobre un caracter, incluido Unicode
NeoBundle 'tpope/vim-characterize'
" transponer lines y bloques de texto (intercambiar columnas y lineas)
NeoBundle 'salsifis/vim-transpose'
" gestion de marcadores
NeoBundle 'kshenoy/vim-signature'
" text-objects
NeoBundle 'kana/vim-textobj-user'
NeoBundle 'kana/vim-textobj-line' " al, il

" }}}

" HTML/CSS {{{

" Permite seleccionar colores mediante una paleta y previsualizarlos en los
" ficheros css/html
NeoBundle 'Rykka/colorv.vim'
NeoBundleLazy 'othree/html5.vim', {'autoload':
            \ {'filetypes': ['html', 'xhttml', 'css']}}
NeoBundleLazy 'joedicastro/vim-sparkup', {'autoload':
            \ {'filetypes': ['html', 'xhttml', 'css']}}

" }}}

" TODOs & FIXMEs {{{

" Gestión los TODOs y los FIXMEs
NeoBundle 'LStinson/TagmaTasks'

" }}}

" GUI {{{

" Barra de estado mas elegante y efectiva
NeoBundle 'Lokaltog/vim-powerline'
" crea buffer temporal desechable
NeoBundle 'vim-scripts/scratch-utility'
" permite des/hacer un zoom a ventana completa de uno de las ventanas
NeoBundle 'vim-scripts/zoomwintab.vim'
" redimensionar ventanas facilmente
NeoBundle 'jimsei/winresizer'

" }}}

" Tmux {{{

" Permite interactuar con Tmux desde vim
NeoBundle 'benmills/vimux'
" Sintaxis del fichero de configuración de tmux
NeoBundle 'vimtopia/vim-tmux'

" }}}

" Ctags {{{

" Muestra las ctags en una barra lateral
NeoBundle 'majutsushi/tagbar'

" }}}

" API Web {{{

NeoBundle 'mattn/webapi-vim'

" }}}

" FIN BUNDLES }}}

" Instalar los plugins automaticamente {{{

" Instalando los plugins por primera vez
if iCanHazNeoBundle == 0
    echo "Installing Bundles, please ignore key map error messages"
    echo ""
    :NeoBundleInstall
endif

" Comprueba que todos los plugins estén instalados, si alguno no lo esta, nos
" preguntara si queremos instalarlo (util para cuando añadimos uno nuevo al
" .vimrc)
NeoBundleCheck

" }}}

filetype plugin indent on      " Sangrado y plugins por tipo de archivo.

" FIN NEOBUNDLE }}}

" CONFIGURACIÓN DE VIM {{{ ===================================================

" Opciones básicas {{{ -------------------------------------------------------

scriptencoding utf-8
set encoding=utf-8              " Establece la codificación a UTF-8.
set ls=2                        " Linea de estado siempre visible.
set go-=T                       " Elimina la barra de herramientas.
set go-=m                       " Elimina el menú.
set go+=rRlLbh                  " Activa todas las barras de desplazamiento.
set go-=rRlLbh                  " Desactiva todas las barras de desplazamiento.
set visualbell                  " Activa la alarma visual.
set cursorline                  " Resalta la linea bajo el cursor.
set fillchars+=vert:│           " Mejora aspecto de la división de ventanas.
set ttyfast                     " Mejora el redibujado de la pantalla.
set title                       " Añade el nombre del fichero al terminal.
set showcmd                     " Muestra comandos incompletos.
set hidden                      " Oculta los bufferes cerrados.
set ruler                       " Establece una regla permanente.
set lazyredraw                  " Redibuja solo cuando es necesario.
set autoread                    " Actualiza cambios realizados fuera de vim.

" }}}

" Búsqueda {{{ ---------------------------------------------------------------

set incsearch                   " Búsqueda incremental.
set showmatch                   " Muestra la pareja de paréntesis.
set hlsearch                    " Resultados de búsquela resaltados.
set smartcase                   " Distinción mayúsculas/minúsculas inteligente.
set ignorecase                  " No distingue entre mayúsculas y minúsculas.

" }}}

" Historia y niveles de deshacer persistentes {{{ ----------------------------

set history=1000
set undofile
set undoreload=1000

" }}}

" Copias de seguridad {{{ ----------------------------------------------------

set backup
set noswapfile
set backupdir=~/.vim/tmp/backup//
set undodir=~/.vim/tmp/undo//
set directory=~/.vim/tmp/swap//
set viminfo+=n~/.vim/tmp/viminfo

" Crear estos directorios si no existen previamente
if !isdirectory(expand(&undodir))
    call mkdir(expand(&undodir), "p")
endif
if !isdirectory(expand(&backupdir))
    call mkdir(expand(&backupdir), "p")
endif
if !isdirectory(expand(&directory))
    call mkdir(expand(&directory), "p")
endif

" }}}

" Wildmenu {{{ ---------------------------------------------------------------

set wildmenu                        " Autocompletado de la linea de comandos.
set wildmode=list:longest,full      " Muestra una lista de todas las opciones.

" set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.bak,*.?~,*.??~,*.???~,*.~      " Backup files
set wildignore+=*.luac                           " Lua byte code
set wildignore+=*.jar                            " java archives
set wildignore+=*.pyc                            " Python byte code
set wildignore+=*.stats                          " Pylint stats

" }}}

" Tabulado, espaciado y wrapping {{{ -----------------------------------------

set expandtab                  " Emplea espacios en vez de tabulados.
set tabstop=4                  " Un Tabulado son cuatro espacios.
set shiftwidth=4               " Numero de espacios para autoindentado.
set softtabstop=4              " Un Tabulado de cuatro espacios.
set autoindent                 " Establece el autoindentado.

" set wrap
set textwidth=80
" set formatoptions=qrn1
set colorcolumn=+1

" }}}

" Hagamos las cosas bien, fuera teclas de desplazamiento {{{ -----------------

nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <left> <nop>
inoremap <right> <nop>

" }}}

" Esquema de color {{{ -------------------------------------------------------

syntax on                      " Habilita el resaltado de sintaxis.
set background=dark            " Establece el fondo oscuro.
set t_Co=256                   " Habilita 256 colores en modo consola.
if has('gui_running')          " Habilita el tema molokai para gvim y vim.
    colorscheme molokai
else
    colorscheme molokai256
endif

" }}}

" Fuente monoespaciada para gui {{{ ------------------------------------------

set guifont=Dejavu\ Sans\ Mono\ for\ Powerline\ 11      " La fuente de texto para gvim.

" }}}

" Reescalar las divisiones cuando se cambia el tamaño de la ventana {{{ ------

au VimResized * exe "normal! \<c-w>="

" }}}

" Mapeo de la teclas <Leader> y <LocalLeader> {{{ -----------------------------

let mapleader=','
let maplocalleader= ' '

" }}}

" Nuevas ventanas {{{ -------------------------------------------------

nnoremap <Leader>v <C-w>v
nnoremap <Leader>h <C-w>s

" }}}

" Cambio rápido entre ventanas {{{ -------------------------------------------

nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" }}}

" Cierre rápido de ventana {{{

nnoremap <Leader>k <C-w>c

" }}}

" Conmutar números de linea {{{ ----------------------------------------------

nnoremap <Leader>l :call ToggleRelativeAbsoluteNumber()<CR>
function! ToggleRelativeAbsoluteNumber()
  if &number
    set relativenumber
  elseif &relativenumber
    set nonumber & norelativenumber
  else
    set number
  endif
endfunction

" }}}

" Mostrar caracteres no imprimibles {{{

nmap <Leader>sh :set list!<CR>
set listchars=tab:→\ ,eol:↵,trail:·,extends:↷,precedes:↶

" }}}

" Plegado {{{ ----------------------------------------------------------------

set foldmethod=marker

" Conmutar el plegado

nnoremap \ za
vnoremap \ za

" }}}

" Copiar/Pegar {{{

" copiar/pegar desde el portapapeles
map <Leader>y "*y
map <Leader>p "*p

" conmutar el paste mode
map <Leader>P :set invpaste<CR>

" }}}

" Edición rápida de varios archivos {{{ ----------------------------------------------

nnoremap <Leader>ev :vsplit $MYVIMRC<cr>
nnoremap <Leader>es :vsplit ~/.vim/UltiSnips/all.snippets<cr>

" }}}

" Autocargar configuración cuando se cambia $MYVIMRC {{{

autocmd! BufWritePost vimrc source %

" }}}

" Revisión de ortografía {{{ ---------------------------------------------------

" Revisar ortografía en español
nmap <Leader>ss :setlocal spell spelllang=es<cr>
" Revisar ortografía en inglés
nmap <Leader>se :setlocal spell spelllang=en<cr>
" Desactivar revisión ortográfica
nmap <Leader>so :setlocal nospell <cr>
" Ir a la siguiente palabra mal escrita
nmap <Leader>sn  ]s
" }}}

" Guardar como root {{{

cmap w!! w !sudo tee % >/dev/null<CR>:e!<CR><CR>

" }}}

" Guardado rápido {{{

nmap <silent> <Leader>w :update<CR>

" }}}

" Eliminar espacios al final de la línea {{{

nmap <Leader>n :%s/\s\+$//<cr>

" }}}

" Emplear Ranger como navegador de ficheros {{{

fun! RangerChooser()
    exec "silent !ranger --choosefile=/tmp/chosenfile " . expand("%:p:h")
    if filereadable('/tmp/chosenfile')
        exec 'edit ' . system('cat /tmp/chosenfile')
        call system('rm /tmp/chosenfile')
    endif
    redraw!
endfun
map <Leader>r :call RangerChooser()<CR>
" }}}

" Mostrar/Ocultar la ventana Quickfix {{{

function! s:QuickfixToggle()
    for i in range(1, winnr('$'))
        let bnum = winbufnr(i)
        if getbufvar(bnum, '&buftype') == 'quickfix'
            cclose
            return
        endif
    endfor
    copen
endfunction
command! ToggleQuickfix call <SID>QuickfixToggle()

nnoremap <silent> <Leader>q :ToggleQuickfix<CR>

" }}}

" Estadisticas de texto {{{

" obtener el numero de lineas, palabras, caracteres y bytes (totales y de la
" posicion actual)
map <Leader>st g<C-G>

" obtener la frecuencia con la que aparece cada palabra para un texto dado
function! WordFrequency() range
  let all = split(join(getline(a:firstline, a:lastline)), '\A\+')
  let frequencies = {}
  for word in all
    let frequencies[word] = get(frequencies, word, 0) + 1
  endfor
  new
  setlocal buftype=nofile bufhidden=hide noswapfile tabstop=20
  for [key,value] in items(frequencies)
    call append('$', key."\t".value)
  endfor
  sort i
endfunction
command! -range=% WordFrequency <line1>,<line2>call WordFrequency()
map <Leader>sw :WordFrequency<CR>

" }}}

" Numero de lineas de codigo {{{

" obtener el numero de lineas de codigo
" map <silent> <Leader>sc :cgete system('cloc --quiet '. bufname("%"))<Bar>copen<CR>
function! LinesOfCode()
    echo system('cloc --quiet '.bufname("%"))
endfunction
"}}}

" desactivar el resaltado de la ultima búsqueda {{{

map <Leader>sq :nohlsearch<CR>

" }}}

" Desplazarse entre ventanas de Vim y Tmux {{{

if exists('$TMUX')
  function! TmuxOrSplitSwitch(wincmd, tmuxdir)
    let previous_winnr = winnr()
    execute "wincmd " . a:wincmd
    if previous_winnr == winnr()
      " The sleep and & gives time to get back to vim so tmux's focus tracking
      " can kick in and send us our ^[[O
      execute "silent !sh -c 'sleep 0.01; tmux select-pane -" . a:tmuxdir . "' &"
      redraw!
    endif
  endfunction
  let previous_title = substitute(system("tmux display-message -p '#{pane_title}'"), '\n', '', '')
  let &t_ti = "\<Esc>]2;vim\<Esc>\\" . &t_ti
  let &t_te = "\<Esc>]2;". previous_title . "\<Esc>\\" . &t_te
  nnoremap <silent> <C-h> :call TmuxOrSplitSwitch('h', 'L')<cr>
  nnoremap <silent> <C-j> :call TmuxOrSplitSwitch('j', 'D')<cr>
  nnoremap <silent> <C-k> :call TmuxOrSplitSwitch('k', 'U')<cr>
  nnoremap <silent> <C-l> :call TmuxOrSplitSwitch('l', 'R')<cr>
else
  map <C-h> <C-w>h
  map <C-j> <C-w>j
  map <C-k> <C-w>k
  map <C-l> <C-w>l
endif

" }}}

" Salir sin guardar nada {{{

nnoremap <Leader>`` :qa!<cr>

" }}}

" FIN CONFIGURACIÓN VIM }}}

" CONFIGURACIÓN DE PLUGINS {{{ ===============================================

" ColorV {{{

let g:colorv_cache_file=$HOME.'/.vim/tmp/vim_colorv_cache'
let g:colorv_cache_fav=$HOME.'/.vim/tmp/vim_colorv_cache_fav'

" }}}

" Commentary {{{ -------------------------------------------------------------

nmap <Leader>c <Plug>CommentaryLine
xmap <Leader>c <Plug>Commentary

augroup plugin_commentary
    au!
    au FileType python setlocal commentstring=#%s
    au FileType htmldjango setlocal commentstring={#\ %s\ #}
    au FileType puppet setlocal commentstring=#\ %s
augroup END

" }}}

" easydigraph {{{

let g:EasyDigraph_nmap = '<Leader>dd'

" }}}

" Fugitive {{{

map <Leader>gs :Gstatus<CR>
map <Leader>gw :Gwrite<CR>
map <Leader>go :Gread<CR>
map <Leader>gr :Gremove<CR>
map <Leader>gm :Gmove<Space>
map <Leader>gc :Gcommit<CR>
map <Leader>gd :Gdiff<CR>
map <Leader>gb :Gblame<CR>
map <Leader>gg :Ggrep<Space>
map <Leader>gB :Gbrowse<CR>
map <Leader>gp :Git push<CR>
map <Leader>gP :Git pull<CR>
map <Leader>gi :Git <Space>
map <Leader>gl :exe "silent Glog <Bar> Unite quickfix"<CR>
map <Leader>gt :!tig<CR>:redraw!<CR>

" }}}

" Gundo {{{ ------------------------------------------------------------------

nnoremap <Leader>u :GundoToggle<CR>

let g:gundo_preview_bottom = 1

" }}}

" indentLine {{{

map <silent> <Leader>sl :IndentLinesToggle<CR>
let g:indentLine_enabled = 0
let g:indentLine_char = '¦'
let g:indentLine_color_term = 239

" }}}

" Neocomplete {{{

let g:neocomplete#enable_at_startup = 1
let g:neocomplete#enable_smart_case = 1
let g:neocomplete#enable_auto_select = 1
let g:neocomplete#max_list = 30
let g:neocomplete#min_keyword_length = 3
let g:neocomplete#sources#syntax#min_keyword_length = 3
let g:neocomplete#data_directory = $HOME.'/.vim/tmp/neocomplete'

" Enable omni completion.
autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

" }}}

" Po.vim {{{ -----------------------------------------------------------------

let g:po_translator = "joe di castro <joe@joedicastro.com>"

" }}}

" Powerline {{{ --------------------------------------------------------------

let g:Powerline_symbols = 'fancy'
let g:Powerline_cache_file = $HOME.'/.vim/tmp/Powerline.cache'

let g:Powerline_theme="unite_status"
let g:Powerline_colorscheme="unite_status"
set noshowmode

" call Pl#Theme#InsertSegment('ws_marker', 'after', 'lineinfo')

" }}}

" PythonMode {{{ -------------------------------------------------------------

let g:pymode_breakpoint_key = '<Leader>B'

let g:pymode_lint_checker = 'pyflakes,pep8,mccabe,pep257'
let g:pymode_lint_ignore = ''

let g:pymode_rope = 1
let g:pymode_rope_guess_project = 0
let g:pymode_rope_vim_completion = 0
let g:pymode_rope_always_show_complete_menu = 1

map <Leader>d :RopeGotoDefinition<CR>
map <Leader>fo :RopeFindOccurrences<CR>

" }}}

" Syntastic {{{

let g:syntastic_python_pylint_exe = "pylint2"

let g:syntastic_error_symbol='✗'
let g:syntastic_warning_symbol='⚠'
let g:syntastic_style_error_symbol  = '⚡'
let g:syntastic_style_warning_symbol  = '⚡'

" }}}

" TagmaTasks {{{

map <silent> <Leader>kk :TagmaTaskToggle<CR>

let g:TagmaTasksPrefix = ''
" let g:TagmaTasksTokens = ['FIXME', 'TODO', 'NOTE', 'XXX', 'COMBAK', '@todo']
" let g:TagmaTasksRegexp = '\<FIXME\|TODO\|NOTE\|XXX\|COMBAK\|@todo\>'

" }}}

" Tagbar {{{ ----------------------------------------------------------------

map <Leader>t :TagbarToggle<CR>

let g:tagbar_autoclose = 1
let g:tagbar_left = 1
let g:tagbar_autofocus = 1
let g:tagbar_sort = 0
let g:tagbar_compact = 1
let g:tagbar_autoshowtag = 1

" }}}

" Unite {{{

" archivos
nnoremap <silent><Leader>o :Unite -start-insert file<CR>
nnoremap <silent><Leader>m :Unite file_mru<CR>
" buffers
nnoremap <silent><Leader>b :Unite buffer<CR>
" busquedas buffer
nnoremap <silent><Leader>f :Unite line -no-split -start-insert -auto-preview<CR>
nnoremap <silent>[menu]w :UniteWithCursorWord -no-split -auto-preview line<CR>
" yankring
nnoremap <silent><Leader>i :Unite history/yank <CR>
" grep
nnoremap <silent><Leader>a :Unite -no-quit grep<CR>
" ayuda
nnoremap <silent> g<C-h> :UniteWithCursorWord help<CR>

" menus {{{
let g:unite_source_menu_menus = {}

" tecla prefijo para menus (se usa para todos los menus de Unite) {{{
nnoremap [menu] <Nop>
nmap <LocalLeader> [menu]
" }}}

" menu menus
nnoremap <silent>[menu]u :Unite menu -start-insert<CR>

" menu ficheros y directorios {{{
let g:unite_source_menu_menus.ficheros = {
    \ 'description' : 'Gestionar ficheros y directorios',
    \}
let g:unite_source_menu_menus.ficheros.command_candidates = [
    \['▷ abrir fichero                                              ⌘ ,o',
        \'Unite -start-insert file'],
    \['▷ abrir fichero reciente                                     ⌘ ,m',
        \'Unite file_mru'],
    \['▷ abrir fichero con busqueda recursiva',
        \'Unite -start-insert file_rec/async'],
    \['▷ crear nuevo fichero',
        \'Unite file/new'],
    \['▷ buscar directorio',
        \'Unite directory'],
    \['▷ buscar directorio reciente',
        \'Unite directory_mru'],
    \['▷ buscar directorio con busqueda recursiva',
        \'Unite directory_rec/async'],
    \['▷ crear nuevo directorio',
        \'Unite directory/new'],
    \['▷ cambiar directorio de trabajo',
        \'Unite -default-action=lcd directory'],
    \['▷ conocer directorio de trabajo',
        \'Unite output:pwd'],
    \['▷ abrir ranger                                               ⌘ ,r',
        \'call RangerChooser()'],
    \]
nnoremap <silent>[menu]o :Unite -start-insert menu:ficheros<CR>
" }}}

" menu busqueda de ficheros {{{
function! OnlyGitFiles()
    cgete system("git ls-files ".input("dir? " )."\\| xargs ack
                \".input("pattern? "))
endfunction
let g:unite_source_menu_menus.grep = {
    \ 'description' : 'Buscar ficheros',
    \}
let g:unite_source_menu_menus.grep.command_candidates = [
    \['▷ grep (ag → ack → grep)                                     ⌘ ,a',
        \'Unite -no-quit grep'],
    \['▷ grep (ack, solo ficheros en el repositorio git)',
        \'exe "call OnlyGitFiles() | Unite quickfix"'],
    \['▷ find',
        \'Unite find'],
    \['▷ locate',
        \'Unite locate'],
    \['▷ vimgrep (muy lento)',
        \'Unite vimgrep'],
    \]
nnoremap <silent>[menu]a :Unite menu:grep<CR>
" }}}

" menu buffers, ventanas y pestañas {{{
let g:unite_source_menu_menus.navegacion = {
    \ 'description' : 'Navegar por buffers, pestañas y ventanas de Vim',
    \}
let g:unite_source_menu_menus.navegacion.command_candidates = [
    \['▷ buffers                                                    ⌘ ,b',
        \'Unite buffer'],
    \['▷ pestañas',
        \'Unite tab'],
    \['▷ ventanas',
        \'Unite window'],
    \['▷ location list',
        \'Unite location_list'],
    \['▷ quickfix',
        \'Unite quickfix'],
    \['▷ redimensionar ventanas                                     ⌘ C-E',
        \'WinResizerStartResize'],
    \['▷ scratch                                                    ⌘ F8',
        \'Scratch'],
    \['▷ zoom                                                       ⌘ ,z',
        \'ZoomWinTabToggle'],
    \['▷ eliminar buffer',
        \'bd'],
    \]
nnoremap <silent>[menu]b :Unite menu:navegacion<CR>
" }}}

" menu busquedas dentro del buffer {{{
let g:unite_source_menu_menus.busquedas = {
    \ 'description' : 'Realizar busquedas dentro del buffer activo',
    \}
let g:unite_source_menu_menus.busquedas.command_candidates = [
    \['▷ buscar linea                                               ⌘ ,f',
        \'Unite -auto-preview -start-insert line'],
    \['▷ buscar encabezados',
        \'Unite -no-split -auto-preview -start-insert outline'],
    \['▷ buscar etiquetas (ctags)                                   ⌘ ,t',
        \'TagbarToggle'],
    \['▷ buscar marcas',
        \'Unite -auto-preview -quick-match mark'],
    \['▷ buscar pliegues',
        \'Unite -no-split -auto-preview -start-insert fold'],
    \['▷ buscar cambios',
        \'Unite change'],
    \['▷ buscar saltos',
        \'Unite jump'],
    \['▷ buscar undos',
        \'Unite undo'],
    \['▷ buscar tareas                                              ⌘ ,kk',
        \'TagmaTaskToggle'],
    \]
nnoremap <silent>[menu]f :Unite -start-insert menu:busquedas<CR>
" }}}

" menu yankring, registros e historia {{{
let g:unite_source_menu_menus.registros = {
    \ 'description' : 'Yankring, registros e historia de Vim',
    \}
let g:unite_source_menu_menus.registros.command_candidates = [
    \['▷ yankring                                                   ⌘ ,i',
        \'Unite history/yank'],
    \['▷ comandos       (historia)                                  ⌘ q:',
        \'Unite history/command'],
    \['▷ busquedas      (historia)                                  ⌘ q/',
        \'Unite history/search'],
    \['▷ registros',
        \'Unite register'],
    \['▷ deshacer       (gundo)                                     ⌘ ,u',
        \'GundoToggle'],
    \]
nnoremap <silent>[menu]i :Unite -start-insert menu:registros<CR>
" }}}

" menu neobundle {{{
let g:unite_source_menu_menus.neobundle = {
    \ 'description' : 'Gestionar NeoBundle',
    \}
let g:unite_source_menu_menus.neobundle.command_candidates = [
    \['▷ neobundle',
        \'Unite neobundle'],
    \['▷ neobundle log',
        \'Unite neobundle/log'],
    \['▷ neobundle lazy',
        \'Unite neobundle/lazy'],
    \['▷ neobundle update',
        \'Unite neobundle/update'],
    \['▷ neobundle search',
        \'Unite neobundle/search'],
    \['▷ neobundle install',
        \'Unite neobundle/install'],
    \['▷ neobundle check',
        \'Unite -no-empty output:NeoBundleCheck'],
    \['▷ neobundle docs',
        \'Unite output:NeoBundleDocs'],
    \['▷ neobundle clean',
        \'Unite output:NeoBundleClean'],
    \['▷ neobundle list',
        \'Unite output:NeoBundleList'],
    \['▷ neobundle direct edit',
        \'NeoBundleDirectEdit'],
    \]
nnoremap <silent>[menu]n :Unite -start-insert menu:neobundle<CR>
" }}}

" menu git {{{
let g:unite_source_menu_menus.git = {
    \ 'description' : 'Gestionar git',
    \}
let g:unite_source_menu_menus.git.command_candidates = [
    \['▷ git status       (Fugitive)                                ⌘ ,gs',
        \'Gstatus'],
    \['▷ git diff         (Fugitive)                                ⌘ ,gd',
        \'Gdiff'],
    \['▷ git commit       (Fugitive)                                ⌘ ,gc',
        \'Gcommit'],
    \['▷ git push         (Fugitive)                                ⌘ ,gp',
        \'Git push'],
    \['▷ git pull         (Fugitive)                                ⌘ ,gP',
        \'Git pull'],
    \['▷ git log          (Fugitive)                                ⌘ ,gl',
        \'exe "silent Glog | Unite quickfix"'],
    \['▷ git blame        (Fugitive)                                ⌘ ,gb',
        \'Gblame'],
    \['▷ git stage        (Fugitive)                                ⌘ ,gw',
        \'Gwrite'],
    \['▷ git checkout     (Fugitive)                                ⌘ ,go',
        \'Gread'],
    \['▷ git rm           (Fugitive)                                ⌘ ,gr',
        \'Gremove'],
    \['▷ git mv           (Fugitive)                                ⌘ ,gm',
        \'exe "Gmove " input("destino: ")'],
    \['▷ git prompt       (Fugitive, salida por buffer)             ⌘ ,gi',
        \'exe "Git! " input("comando git: ")'],
    \['▷ git cd           (Fugitive)',
        \'Gcd'],
    \['▷ tig                                                        ⌘ ,gt',
        \'!tig'],
    \]
nnoremap <silent>[menu]g :Unite -start-insert menu:git<CR>
" }}}

" menu python {{{
" TODO: terminar menu
let g:unite_source_menu_menus.python = {
    \ 'description' : 'Gestionar python',
    \}
let g:unite_source_menu_menus.python.command_candidates = [
    \['▷ syntastic check',
        \'SyntasticCheck'],
    \['▷ syntastic errors',
        \'Errors'],
    \['▷ contar lineas de codigo',
        \'Unite output:call\\ LinesOfCode()'],
    \]
nnoremap <silent>[menu]p :Unite menu:python<CR>
" }}}

" menu markdown {{{
let g:unite_source_menu_menus.markdown = {
    \ 'description' : 'Markdown Extra Preview',
    \}
let g:unite_source_menu_menus.markdown.command_candidates = [
    \['▷ previsualizar',
        \'Me'],
    \['▷ refrescar',
        \'Mer'],
    \]
nnoremap <silent>[menu]k :Unite menu:markdown<CR>
" }}}

" menu sesiones {{{
let g:unite_source_menu_menus.sesiones = {
    \ 'description' : 'Gestionar sesiones de Vim',
    \}
let g:unite_source_menu_menus.sesiones.command_candidates = [
    \['▷ cargar sesion',
        \'Unite session'],
    \['▷ guardar sesion (default)',
        \'UniteSessionSave'],
    \['▷ guardar sesion (personalizada)',
        \'exe "UniteSessionSave " input("nombre: ")'],
    \]
nnoremap <silent>[menu]s :Unite menu:sesiones<CR>
" }}}

" menu favoritos {{{
let g:unite_source_menu_menus.favoritos = {
    \ 'description' : 'Gestionar favoritos de Vim',
    \}
let g:unite_source_menu_menus.favoritos.command_candidates = [
    \['▷ añadir a favoritos',
        \'UniteBookmarkAdd'],
    \['▷ abrir favoritos',
        \'Unite bookmark:*'],
    \]
nnoremap <silent>[menu]h :Unite menu:favoritos<CR>
" }}}

" menu varios {{{
let g:unite_source_menu_menus.varios = {
    \ 'description' : 'Varios comandos',
    \}
let g:unite_source_menu_menus.varios.command_candidates = [
    \['▷ escoger esquema de color',
        \'Unite colorscheme -auto-preview'],
    \['▷ frecuencia de cada palabra en el texto',
        \'WordFrequency'],
    \['▷ muestra los digrafos disponibles',
        \'digraphs'],
    \['▷ selector de color                                          ⌘ ,cd',
        \'ColorVPicker'],
    \['▷ matar procesos',
        \'Unite -default-action=sigkill -start-insert process'],
    \['▷ atajos de teclado',
        \'Unite mapping -start-insert'],
    \['▷ fuentes de unite',
        \'Unite source'],
    \['▷ establecer el tipo de fichero',
        \'Unite -start-insert filetype'],
    \['▷ ayuda de vim',
        \'Unite -start-insert help'],
    \['▷ comandos de vim',
        \'Unite -start-insert command'],
    \['▷ funciones de vim',
        \'Unite -start-insert function'],
    \['▷ lanzar ejecutable (dmenu like)',
        \'Unite -start-insert launcher'],
    \['▷ runtimepath de vim',
        \'Unite -start-insert runtimepath'],
    \['▷ salida de comando de vim',
        \'Unite output'],
    \]
" }}}

nnoremap <silent>[menu]v :Unite menu:varios -start-insert<CR>
" }}}

call unite#filters#matcher_default#use(['matcher_fuzzy'])
call unite#filters#sorter_default#use(['sorter_rank'])

let g:unite_source_history_yank_enable = 1
let g:unite_enable_start_insert = 0
let g:unite_enable_short_source_mes = 0
let g:unite_force_overwrite_statusline = 0
let g:unite_prompt = '>>> '
let g:unite_marked_icon = '✓'
let g:unite_winheight = 15
let g:unite_update_time = 200
let g:unite_split_rule = 'botright'
let g:unite_data_directory = '~/.vim/tmp/unite'
let g:unite_source_buffer_time_format = '(%d-%m-%Y %H:%M:%S) '
let g:unite_source_file_mru_time_format = '(%d-%m-%Y %H:%M:%S) '
let g:unite_source_directory_mru_time_format = '(%d-%m-%Y %H:%M:%S) '

if executable('ag')
    let g:unite_source_grep_command='ag'
    let g:unite_source_grep_default_opts='--nocolor --nogroup -a -S'
    let g:unite_source_grep_recursive_opt=''
    let g:unite_source_grep_search_word_highlight = 1
elseif executable('ack')
    let g:unite_source_grep_command='ack'
    let g:unite_source_grep_default_opts='--no-group --no-color'
    let g:unite_source_grep_recursive_opt=''
    let g:unite_source_grep_search_word_highlight = 1
endif

" navegacion por la ventana de unite mas comoda
autocmd FileType unite call s:unite_settings()
function! s:unite_settings()
  nmap <buffer> <Esc><Esc> <Plug>(unite_exit)
  imap <buffer> <Esc><Esc> <Plug>(unite_exit)
  imap <buffer> <Esc> <Plug>(unite_insert_leave)
endfunction

" }}}

" Utl {{{

map <Leader>j :Utl <CR><Bar>:redraw!<CR>

let g:utl_cfg_hdl_scm_http_system = "silent !firefox %u &"
let g:utl_cfg_hdl_mt_application_pdf = 'silent :!zathura %p &'
let g:utl_cfg_hdl_mt_image_jpeg = 'silent :!sxiv %p &'
let g:utl_cfg_hdl_mt_image_gif = 'silent :!sxiv %p &'
let g:utl_cfg_hdl_mt_image_png = 'silent :!sxiv %p &'

" }}}

" Vim-markdown-extra-preview {{{

" map <LocalLeader>mp :Me<CR>
" map <LocalLeader>mr :Mer<CR>

" let g:VMEPextensions = ['extra', 'codehilite']
" let g:VMEPhtmlreader= '/usr/bin/chromium-browser'

" }}}

" vimux {{{

let g:VimuxUseNearestPane = 1

" ejecutar el buffer actual con el interprete de python
map <Leader>xr :w<CR>:call VimuxRunCommand("clear;python2 " . bufname("%"))<CR>
map <Leader>xt :w<CR>:call VimuxRunCommand("clear;time python2 " . bufname("%"))<CR>
map <Leader>xp :w<CR>:call VimuxRunCommand("clear;time pypy " . bufname("%"))<CR>

map <Leader>xc :VimuxPromptCommand<CR>
map <Leader>xl :VimuxRunLastCommand<CR>
map <Leader>xs :VimuxInterruptRunner<CR>
map <Leader>xi :VimuxInspectRunner<CR>
map <Leader>xz :VimuxCloseRunner<CR>

" }}}

" Virtualenv {{{

" TODO

" }}}

" winresizer {{{

let s:default_start_key = '<C-E>'

" }}}

" zoomwintab {{{

map <Leader>z :ZoomWinTabToggle<CR>

" }}}

" FIN CONFIGURACION PLUGINS }}}

" TIPOS DE ARCHIVO  {{{ ======================================================

" JSON {{{ -------------------------------------------------------------------

" autocmd BufNewFile,BufRead *.json set ft=javascript
autocmd BufNewFile,BufRead *.json set ft=json

augroup json_autocmd
  autocmd!
  autocmd FileType json set autoindent
  autocmd FileType json set formatoptions=tcq2l
  autocmd FileType json set textwidth=78 shiftwidth=2
  autocmd FileType json set softtabstop=2 tabstop=8
  autocmd FileType json set expandtab
  autocmd FileType json set foldmethod=syntax
augroup END

" }}}

" LUA {{{

au BufRead,BufNewFile rc.lua setlocal foldmethod=marker

" }}}

" MARKDOWN {{{

" markdown filetype file
au BufRead,BufNewFile *.{md,mdown,mkd,mkdn,markdown,mdwn} set filetype=markdown
autocmd FileType markdown NeoBundleSource vim-markdown
autocmd FileType markdown NeoBundleSource vim-markdown-extra-preview

" Después de N segundos (definidos por `updatetime`) sin actividad en el buffer,
" se salva el archivo. Útil para previsualizar los artículos de Pelican.
augroup ft_markdown
    au!
    au CursorHold,CursorHoldI *.{md,mdown,mkd,mkdn,markdown,mdwn} w
augroup END

let g:updatetime=5
" }}}

" FIN TIPOS DE ARCHIVO }}}

" vim:foldmethod=marker
